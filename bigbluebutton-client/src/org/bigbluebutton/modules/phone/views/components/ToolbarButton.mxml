<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton open source conferencing system - http://www.bigbluebutton.org
  
  Copyright (c) 2010 BigBlueButton Inc. and by respective authors (see below).
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 2.1 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
-->

<mx:Button xmlns:mx="http://www.adobe.com/2006/mxml"
	icon="{phoneIcon}" click="startPhone()"
	xmlns:mate="http://mate.asfusion.com/"
	toolTip="{ResourceUtil.getInstance().getString('bbb.toolbar.phone.toolTip')}"
	implements="org.bigbluebutton.common.IBbbToolbarComponent">
	
	<mate:Listener type="{CallDisconnectedEvent.CALL_DISCONNECTED_EVENT}" method="handleCallDisconnectedEvent"/>
	<mate:Listener type="{BBBEvent.JOIN_VOICE_CONFERENCE}" method="handleBBBJoinConferenceEvent"/>
	<mate:Listener type="MIC_SETTINGS_CLOSED" method="handleMicSettingsClosedEvent"/>
	
	<mx:Script>
		<![CDATA[
			import com.asfusion.mate.events.Dispatcher;
			
			import org.bigbluebutton.common.LogUtil;
			import org.bigbluebutton.core.BBB;
			import org.bigbluebutton.main.events.BBBEvent;
			import org.bigbluebutton.main.views.MainToolbar;
			import org.bigbluebutton.modules.phone.PhoneOptions;
			import org.bigbluebutton.modules.phone.events.CallConnectedEvent;
			import org.bigbluebutton.modules.phone.events.CallDisconnectedEvent;
			import org.bigbluebutton.modules.phone.views.assets.Images;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			
			private var images:Images = new Images();
			private var dispatcher:Dispatcher = new Dispatcher();
			 
			[Bindable] public var phoneIcon:Class = images.headset_icon;
			
			private function startPhone():void {
				if (this.selected) {
					var vxml:XML = BBB.getConfigForModule("PhoneModule");
					var phoneOptions:PhoneOptions = new PhoneOptions();
					if (vxml != null) {
						phoneOptions.showButton = (vxml.@showButton.toString().toUpperCase() == "TRUE") ? true : false;
						phoneOptions.autoJoin = (vxml.@autoJoin.toString().toUpperCase() == "TRUE") ? true : false;
						phoneOptions.skipCheck = (vxml.@skipCheck.toString().toUpperCase() == "TRUE") ? true : false;
					}
					this.enabled = false;
					if (phoneOptions.skipCheck || noMicrophone()) {
						/*
						 * If the user had no mic, let her join but she'll just be listening.	
						*/
						var joinEvent:BBBEvent = new BBBEvent("JOIN_VOICE_CONFERENCE_EVENT");
						joinEvent.payload['useMicrophone'] = true;
						dispatcher.dispatchEvent(joinEvent);
					} else {
						dispatcher.dispatchEvent(new BBBEvent("SHOW_MIC_SETTINGS"));
					}
				} else {
					dispatcher.dispatchEvent(new BBBEvent("LEAVE_VOICE_CONFERENCE_EVENT"));
				}
				
				
				var t:Timer = new Timer(3000, 1);
				t.addEventListener(TimerEvent.TIMER, onTimer);
				t.start();
			}
			
			private function noMicrophone():Boolean {
				return ((Microphone.getMicrophone() == null) || (Microphone.names.length == 0) 
					|| ((Microphone.names.length == 1) && (Microphone.names[0] == "Unknown Microphone")));
			}
			
			private function onTimer(e:TimerEvent):void{
				this.enabled = true;
			}
			
			private function handleCallDisconnectedEvent(event:CallDisconnectedEvent):void {
				this.selected = false;
			}
			
			private function handleMicSettingsClosedEvent(event:BBBEvent):void {
				this.selected = false;
			}
			
			private function handleBBBJoinConferenceEvent(event:BBBEvent):void {
				var joinEvent:BBBEvent = new BBBEvent("JOIN_VOICE_CONFERENCE_EVENT");
				joinEvent.payload['useMicrophone'] = false;
				dispatcher.dispatchEvent(joinEvent);
			}
			
			//For whatever reason the tooltip does not update when localization is changed dynamically. Overrideing it here
			override protected function resourcesChanged():void{
				this.toolTip = ResourceUtil.getInstance().getString('bbb.toolbar.phone.toolTip');
			}
			
			public function getAlignment():String{
				return MainToolbar.ALIGN_LEFT;
			}
		]]>
	</mx:Script>
</mx:Button>
